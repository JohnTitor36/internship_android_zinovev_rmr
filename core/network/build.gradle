apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file('buildsystem/versions.gradle')
apply from: rootProject.file('buildsystem/dependencies.gradle')

def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretProperties = new Properties()

// Try reading secrets from a file
if (secretsPropertiesFile.exists()) {
    secretProperties.load(new FileInputStream(secretsPropertiesFile))
}

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name

        consumerProguardFiles 'consumer-rules.pro'
        buildConfigField("String", "BASE_URL", "\"https://api.themoviedb.org/3\"")
        buildConfigField("String", "MOVIES_API_KEY", "${secretProperties['movies_api_key']}")
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {

    implementation(
            deps.kotlin.stdlib,
            deps.android.appcompat,
            deps.network.retrofit2,
            deps.network.moshi,
            deps.dagger.dagger
    )

    kapt(deps.dagger.compiler, deps.network.moshicodegen)
}
