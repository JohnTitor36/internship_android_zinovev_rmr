apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "scabbard.gradle"
apply from: 'https://raw.githubusercontent.com/JakeWharton/SdkSearch/master/gradle/projectDependencyGraph.gradle'

apply from: rootProject.file('buildsystem/versions.gradle')
apply from: rootProject.file('buildsystem/dependencies.gradle')

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId versions.appId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

scabbard {
    enabled true
}

dependencies {
    implementation project(":core")
    implementation project(":core:network")
    implementation project(":core:preferences")
    implementation project(":features:login")
    implementation project(":features:movies")
    implementation project(":features:user")

    implementation(
            deps.kotlin.stdlib,
            deps.android.appcompat,
            deps.android.ktx,
            deps.android.fragment,
            deps.android.constraint,
            deps.material.material,
            deps.navigation.fragment,
            deps.navigation.ui,
            deps.tools.timber,
            deps.dagger.dagger
    )

    kapt(deps.dagger.compiler)

    testImplementation(deps.test.junit)
    androidTestImplementation(deps.androidTest.junit, deps.androidTest.espresso)
}
